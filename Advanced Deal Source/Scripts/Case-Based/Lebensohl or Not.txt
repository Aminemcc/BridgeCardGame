//----- Lebensohl or Not-----//
//1NT-(2D/H/S)-?
//Overcalls : MM, Mm, no mm / one suit / Penalty
//(2D/H/S)-X-(P)-?

//-- Setting --//
//Player who can receive the sequences
north_south = 1
east_west = 0

open1NT = 1
open2X = 1

//- 1NT Setting -//
has5M = 1
min1NTOpen = 15
max1NTOpen = 17

minTotalHCP = 17
maxTotalHCP = 36
//- 1NT Setting -//

//-2X Setting -//

// 0 : off, 1 : on, strongX : HCP 18+
strongX = 1

//-2X Setting -//

//-- Setting --//

//1NT Script//
north1NTHCP = min1NTOpen <= hcp(north) && hcp(north) <= max1NTOpen && north_south
south1NTHCP = min1NTOpen <= hcp(south) && hcp(south) <= max1NTOpen && north_south
east1NTHCP = min1NTOpen <= hcp(east) && hcp(east) <= max1NTOpen && east_west
west1NTHCP = min1NTOpen <= hcp(west) && hcp(west) <= max1NTOpen && east_west

nsHCP = minTotalHCP <= (hcp(north) + hcp(south)) && (hcp(north) + hcp(south)) <= maxTotalHCP
ewHCP = minTotalHCP <= (hcp(east) + hcp(west)) && (hcp(east) + hcp(west)) <= maxTotalHCP

north1NTDist = shape(north, any 4432 + any 4333 + any 5332) && ( has5M || shape(north, xxxx - 5xxx - x5xx) ) && north1NTHCP && nsHCP
east1NTDist = shape(east, any 4432 + any 4333 + any 5332) && ( has5M || shape(east, xxxx - 5xxx - x5xx) ) && east1NTHCP && ewHCP
south1NTDist = shape(south, any 4432 + any 4333 + any 5332) && ( has5M || shape(south, xxxx - 5xxx - x5xx) ) && south1NTHCP && nsHCP
west1NTDist = shape(west, any 4432 + any 4333 + any 5332) && ( has5M || shape(west, xxxx - 5xxx - x5xx) ) && west1NTHCP && ewHCP

north1NTOvc = cccc(north) >= 1100 && shape(north, any 55xx - xx55) && hcp(north) <= 10 && west1NTDist
south1NTOvc = cccc(south) >= 1100 && shape(south, any 55xx - xx55) && hcp(south) <= 10 && east1NTDist
east1NTOvc = cccc(east) >= 1100 && shape(east, any 55xx - xx55) && hcp(east) <= 10 && north1NTDist
west1NTOvc = cccc(west) >= 1100 && shape(west, any 55xx - xx55) && hcp(west) <= 10 && south1NTDist

condition1NT = north1NTOvc || east1NTOvc || south1NTOvc || west1NTOvc

//2X Script//
north2XDist = cccc(north) >= 1200 && shape(north, any 6xxx - 64xx - 46xx - any 65xx - any 66xx - any 75xx) && club(north) < 6 && 6 <= hcp(north) && hcp(north) <= 9 && north_south
south2XDist = cccc(south) >= 1200 && shape(south, any 6xxx - 64xx - 46xx - any 65xx - any 66xx - any 75xx) && club(south) < 6 && 6 <= hcp(south) && hcp(south) <= 9 && north_south
west2XDist = cccc(west) >= 1200 && shape(west, any 6xxx - 64xx - 46xx - any 65xx - any 66xx - any 75xx) && club(west) < 6 && 6 <= hcp(west) && hcp(west) <= 9 && east_west
east2XDist = cccc(east) >= 1200 && shape(east, any 6xxx - 64xx - 46xx - any 65xx - any 66xx - any 75xx) && club(east) < 6 && 6 <= hcp(east) && hcp(east) <= 9 && east_west

northOvcHCP = 13 <= hcp(north) && hcp(north) <= 16
northOvcXShape = shape(north, any 4333 + any 4432 + any 54xx - any 5422) && ( (hearts(north) < 5 || spades(north) == 0) && (spades(north) < 5 || hearts(north) == 0)) && 
	( ( hearts(west) == 6 && spades(north) >= hearts(north) && diamonds(north) >= hearts(north) && clubs(north) >= hearts(north) ) || 
	( spades(west) == 6 && hearts(north) >= spades(north) && diamonds(north) >= spades(north) && clubs(north) >= spades(north) ) )
northOvcX = ( northOvcHCP && northOvcXShape && 
	( (hearts(west) == 6 && hcp(north, heart) <= 1 && heart(north) <= 2 ) || (spades(west) == 6 && hcp(north, spade) <= 1) && spade(north) <= 2 ) || (hcp(north) >= 19 && strongX))

southOvcHCP = 13 <= hcp(south) && hcp(south) <= 16
southOvcXShape = shape(south, any 4333 + any 4432 + any 54xx - any 5422) && ( (hearts(south) < 5 || spades(south) == 0) && (spades(south) < 5 || hearts(south) == 0)) && 
	( ( hearts(east) == 6 && spades(south) >= hearts(south) && diamonds(south) >= hearts(south) && clubs(south) >= hearts(south) ) || 
	( spades(east) == 6 && hearts(south) >= spades(south) && diamonds(south) >= spades(south) && clubs(south) >= spades(south) ) )
southOvcX = ( southOvcHCP && southOvcXShape && 
	( (hearts(east) == 6 && hcp(south, heart) <= 1 && heart(south) <= 2 ) || (spades(east) == 6 && hcp(south, spade) <= 1) && spade(south) <= 2 ) || (hcp(south) >= 19 && strongX))

westOvcHCP = 13 <= hcp(west) && hcp(west) <= 16
westOvcXShape = shape(west, any 4333 + any 4432 + any 54xx - any 5422) && ( (hearts(west) < 5 || spades(west) == 0) && (spades(west) < 5 || hearts(west) == 0)) && 
	( ( hearts(south) == 6 && spades(west) >= hearts(west) && diamonds(west) >= hearts(west) && clubs(west) >= hearts(west) ) || 
	( spades(south) == 6 && hearts(west) >= spades(west) && diamonds(west) >= spades(west) && clubs(west) >= spades(west) ) )
westOvcX = ( westOvcHCP && westOvcXShape && 
	( (hearts(south) == 6 && hcp(west, heart) <= 1 && heart(west) <= 2 ) || (spades(south) == 6 && hcp(west, spade) <= 1) && spade(west) <= 2 ) || (hcp(west) >= 19 && strongX))

eastOvcHCP = 13 <= hcp(east) && hcp(east) <= 16
eastOvcXShape = shape(east, any 4333 + any 4432 + any 54xx - any 5422) && ( (hearts(east) < 5 || spades(east) == 0) && (spades(east) < 5 || hearts(east) == 0)) && 
	( ( hearts(north) == 6 && spades(east) >= hearts(east) && diamonds(east) >= hearts(east) && clubs(east) >= hearts(east) ) || 
	( spades(north) == 6 && hearts(east) >= spades(east) && diamonds(east) >= spades(east) && clubs(east) >= spades(east) ) )
eastOvcX = ( eastOvcHCP && eastOvcXShape && 
	( (hearts(north) == 6 && hcp(east, heart) <= 1 && heart(east) <= 2 ) || (spades(north) == 6 && hcp(east, spade) <= 1) && spade(east) <= 2 ) || (hcp(east) >= 19 && strongX))


condition2X = (north2XDist && eastOvcX) || (east2XDist && southOvcX) || (south2XDist && westOvcX) || (west2XDist && northOvcX)

generate 100
condition (condition1NT && open1NT) || (condition2X && open2X)