//-1NT Opening-//

//--Opener Setting Start--//
minHcpOpen = 15
maxHcpOpen = 17

openNo5 = 1
open5M = 1
open5m = 1
semiBal = 0
//--Opener Setting End--//

//--North South Setting Start--//
minTotalHcp = 15
maxTotalHcp = 40
minFitM = 0
minFitm = 0
//--North South Setting End--//

//--Responder Setting Start--//
minHcpRes = 0
maxHcpRes = 40

alwaysVoid = 0
resBalNo5 = 1
res3suiter = 1
res5M = 1
res54M = 1
res6M = 1
res7Mup = 1
res5m = 1
res54m = 1
res6m = 1
res7mup = 1
resMM = 1
resmm = 1
resMm = 1
//--Responder Setting End--//

//--Overcall Setting Start--//
ovcSet = 1
minHcpOvc = 8
maxHcpOvc = 11
minFitOvc = 9

ovcElse = 0
ovc6M = 1
ovcMM = 1
ovcmm = 1
ovcMm = 1
ovc7x = 1
ovc8xup = 1
//--Overcall Setting End--//

//--Script Start--//

//---North Start---//
na = minHcpOpen <= hcp(north) && hcp(north) <= maxHcpOpen
nb = shape(north, any 5332 + any 4432 + any 4333 + any 5422)
nc = !shape(north, any 4432 + any 4333) || openNo5
nd = !shape(north, 5xxx + x5xx - any 5422) || open5M
ne = !shape(north, xx5x + xxx5 - any 5422) || open5m
nf = !shape(north, any 5422) || semiBal

nz = na && nb && nc && nd && ne && nf
//---North End---//

//---North South Start---//
nsHcp = hcp(north) + hcp(south)
nsM = (hearts(north) + hearts(south) >= minFitM) || (spades(north) + spades(south) >= minFitM)
nsm = (diamonds(north) + diamonds(south) >= minFitm) || (clubs(north) + clubs(south) >= minFitm)
nsa = minTotalHcp <= nsHcp && nsHcp <= maxTotalHcp

nsz = nsa && nsM && nsm
//---North South End---//

//---South Start---//
s5M = shape(south, 5xxx + x5xx)
s5m = shape(south, xx5x + xxx5)
s54M = shape(south, 54xx + 45xx)
s54m = shape(south, xx54 + xx45)
s6M = shape(south, 6xxx + x6xx)
s6m = shape(south, xx6x + xxx6)
s7Mup = spades(south) >= 7 || hearts(south) >= 7
s7mup = diamonds(south) >= 7 || clubs(south) >= 7
s2suiter = shape(south, any 55xx + any 65xx + any 6610 + any 7510 + any 7600)
sMM = shape(south, 55xx + 65xx + 56xx + 66xx + 75xx + 57xx + 7600 + 6700)
smm = shape(south, xx55 + xx65 + xx56 + xx66 + xx75 + xx57 + 0076 + 0067)
sVoid = shape(south, any 0xxx)

sa = minHcpRes <= hcp(south) && hcp(south) <= maxHcpRes
sb = resBalNo5 || !shape(south, any 4432 + any 4333)
sc = res3suiter || !shape(south, any 4441)
sd = res5M || !(s5M && !s54M && !s2suiter)
se = res54M || !s54M
sf = res6M || !(s6M && !s2suiter)
sg = res5m || !(s5m && !s54m && !s2suiter)
sh = res54m || !s54m
si = res6m || !(s6m && !s2suiter)
sj = resMM || !sMM
sk = resmm || !smm
sl = resMm || !(s2suiter && !sMM && !smm)
sm = res7Mup || !(s7Mup && !s2suiter)
sn = res7mup || !(s7mup && !s2suiter)
so = !alwaysVoid || (alwaysVoid && sVoid)

sz = sa && sb && sc && sd && se && sf && sg && sh && si && sj && sk && sl && sm && sn && so
//---South End---//

//---East Start---//
e6M = shape(east, 6xxx + x6xx)
e7x = shape(east, any 7xxx - any 7600)
e8xup = spades(east) >= 8 || hearts(east) >= 8 || diamonds(east) >= 8 || clubs(east) >= 8
eMM = shape(east, 55xx + 65xx + 56xx + 66xx + 76xx + 67xx)
emm = shape(east, xx55 + xx65 + xx56 + xx66 + xx76 + xx67)
eMm = shape(east, any 55xx + any 65xx + any 6610 + any 7600) && !ovcMM && !ovcmm

ea = minHcpOvc <= hcp(east) && hcp(east) <= maxHcpOvc
eb = ovc6M || !(e6M && !eMM && !eMm)
ec = ovcMM || !eMM
ed = ovcmm || !emm
ee = ovcMm || !eMm
ef = ovc7x || !e7x
eg = ovc8xup || !e8xup
eh = ovcElse || !(!e6M && !e7x && !e8xup && !eMM && !emm && !eMm)

ewM = (spades(east) + spades(west) >= minFitOvc) || (hearts(east) + hearts(west) >= minFitOvc)
ewm = (diamonds(east) + diamonds(west) >= minFitOvc) || (clubs(east) + clubs(west) >= minFitOvc)
ez = !ovcSet || ((ea && eb && ec && ed && ee && ef && eg && eh) && (ewM || ewm))

//---East End---//

condition nz && sz && nsz && ez