//-1C Opening Precision-//

//--Opener Setting Start--//
minHcpOpen = 16
maxHcpOpen = 30
bal_16_17 = 0

minMajor = 0
minminor = 0
alwaysVoidOpen = 0

openElse = 1
openBal = 1
open4441 = 1
open6x = 1
open7x = 1
open8xup = 1
open54 = 1
open55 = 1
open65 = 1
open66 = 1
//--Opener Setting End--//

//--North South Setting Start--//
minTotalHcp = 16
maxTotalHcp = 35
minFitM = 0
minFitm = 0
//--North South Setting End--//

//--Responder Setting Start--//
minHcpRes = 0
maxHcpRes = 20

alwaysVoidRes = 0
resBalNo5 = 1
res3suiter = 1
res5M = 1
res54M = 1
res6M = 1
res7Mup = 1
res5m = 1
res54m = 1
res6m = 1
res7mup = 1
resMM = 1
resmm = 1
resMm = 1
//-Responder Setting Start--//

//--Overcall Setting Start--//
ovcSet = 1
minHcpOvc = 8
maxHcpOvc = 11
minFitOvc = 9

ovcElse = 0
ovc5M = 1
ovc5m = 1
ovc6M = 1
ovc6m = 1
ovcMM = 1
ovcmm = 1
ovcMm = 1
ovc7x = 1
ovc8xup = 1
//--Overcall Setting End--//

//--Script Start--//

//---North Start---//
nBal = shape(north, any 4432 + any 4333 + any 5332)
n4441 = shape(north, any 4441)
n6x = shape(north, any 6xxx)
n7x = shape(north, any 7xxx)
n8xup = shape(north, any 8xxx + any 9xxx)
n54 = shape(north, any 54xx)
n55 = shape(north, any 55xx)
n65 = shape(north, any 65xx)
n66 = shape(north, any 66xx)

na = minHcpOpen <= hcp(north) && hcp(north) <= maxHcpOpen
nb = openBal || !(nBal && (!bal_16_17 || (hcp(north) >= 18)))
nc = open4441 || !n4441
nd = open6x || !(n6x && !n65 && !n66)
ne = open7x || !n7x
nf = open8xup || !n8xup
ng = open54 || !n54
nh = open55 || !n55
ni = open65 || !n65
nj = open66 || !n66
nk = openElse || !(!nBal && !n4441 && !n6x && !n7x && !n8xup && !n54 && !n55 && !n65 && !n66)
nnM = spades(north) >= minMajor || hearts(north) >= minMajor
nnm = diamonds(north) >= minminor || clubs(north) >= minminor
nnV = !alwaysVoidOpen || (alwaysVoidOpen && shape(north, any 0xxx))

nz = na && nb && nc && nd && ne && nf && ng && nh && ni && nj && nk && nnM && nnm && nnV
//---North End---//

//---North South Start---//
nsHcp = hcp(north) + hcp(south)
nsM = (hearts(north) + hearts(south) >= minFitM) || (spades(north) + spades(south) >= minFitM)
nsm = (diamonds(north) + diamonds(south) >= minFitm) || (clubs(north) + clubs(south) >= minFitm)
nsa = minTotalHcp <= nsHcp && nsHcp <= maxTotalHcp

nsz = nsa && nsM && nsm
//---North South End---//

//---South Start---//
s5M = shape(south, 5xxx + x5xx)
s5m = shape(south, xx5x + xxx5)
s54M = shape(south, 54xx + 45xx)
s54m = shape(south, xx54 + xx45)
s6M = shape(south, 6xxx + x6xx)
s6m = shape(south, xx6x + xxx6)
s7Mup = spades(south) >= 7 || hearts(south) >= 7
s7mup = diamonds(south) >= 7 || clubs(south) >= 7
s2suiter = shape(south, any 55xx + any 65xx + any 6610 + any 7510 + any 7600)
sMM = shape(south, 55xx + 65xx + 56xx + 66xx + 75xx + 57xx + 7600 + 6700)
smm = shape(south, xx55 + xx65 + xx56 + xx66 + xx75 + xx57 + 0076 + 0067)
sVoid = shape(south, any 0xxx)

sa = minHcpRes <= hcp(south) && hcp(south) <= maxHcpRes
sb = resBalNo5 || !shape(south, any 4432 + any 4333)
sc = res3suiter || !shape(south, any 4441)
sd = res5M || !(s5M && !s54M && !s2suiter)
se = res54M || !s54M
sf = res6M || !(s6M && !s2suiter)
sg = res5m || !(s5m && !s54m && !s2suiter)
sh = res54m || !s54m
si = res6m || !(s6m && !s2suiter)
sj = resMM || !sMM
sk = resmm || !smm
sl = resMm || !(s2suiter && !sMM && !smm)
sm = res7Mup || !(s7Mup && !s2suiter)
sn = res7mup || !(s7mup && !s2suiter)
so = !alwaysVoidRes || (alwaysVoidRes && sVoid)

sz = sa && sb && sc && sd && se && sf && sg && sh && si && sj && sk && sl && sm && sn && so
//---South End---//

//---East Start---//
e5M = shape(east, 5xxx + x5xx)
e5m = shape(east, xx5x + xxx5)
e6M = shape(east, 6xxx + x6xx)
e6m = shape(east, xx6x + xxx6)
e7x = shape(east, any 7xxx - any 7600)
e8xup = spades(east) >= 8 || hearts(east) >= 8 || diamonds(east) >= 8 || clubs(east) >= 8
eMM = shape(east, 55xx + 65xx + 56xx + 66xx + 76xx + 67xx)
emm = shape(east, xx55 + xx65 + xx56 + xx66 + xx76 + xx67)
eMm = shape(east, any 55xx + any 65xx + any 6610 + any 7600) && !ovcMM && !ovcmm

ea = minHcpOvc <= hcp(east) && hcp(east) <= maxHcpOvc
eb = ovc5M || !(e5M && !eMM && !eMm)
ec = ovc5m || !(e5m && !emm && !eMm)
ed = ovc6M || !(e6M && !eMM && !eMm)
ee = ovc6m || !(e6m && !emm && !eMm)
ef = ovcMM || !eMM
eg = ovcmm || !emm
eh = ovcMm || !eMm
ei = ovc7x || !e7x
ej = ovc8xup || !e8xup
ek = ovcElse || !(!e5M && !e5m && !e6M && !e6m && !e7x && !e8xup && !eMM && !emm && !eMm)

ewM = (spades(east) + spades(west) >= minFitOvc) || (hearts(east) + hearts(west) >= minFitOvc)
ewm = (diamonds(east) + diamonds(west) >= minFitOvc) || (clubs(east) + clubs(west) >= minFitOvc)
ez = !ovcSet || ((ea && eb && ec && ed && ee && ef && eg && eh && ei && ej && ek) && (ewM || ewm))

//---East End---//

condition nz && nsz && sz && ez