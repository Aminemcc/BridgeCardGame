//----- Open 1D -----//
//1D = 12-15 2+D
//Gak bisa open 1H, 1S, 2C, 1NT

//---Setting---//
//Minimum HCP North + South
min_hcp_NS = 21
max_hcp_NS = 40

//set ke 0 jika ingin kemungkinan musuh bid kecil
can_bid_EW = 0
//---Setting---//

//Rentang HCP South
southHCP = 12 <= hcp(south) && hcp(south) <= 15

southHCP_12_14 = 12 <= hcp(south) && hcp(south) <= 14
southHCP_15 = hcp(south) == 15

//Distribusi South
southH = hearts(south) >= 5
southS = spades(south) >= 5
southC = clubs(south) >= 6
southBalance = shape(south, any 4333 + any 4432 + any 5332 - 5xxx - x5xx)
southBannedDist = shape(south, 4405 + 4405 + 4x5x + x45x)
southDist = (southHCP_12_14 && southBalance) || 
	( ( (southHCP_15 && !southBalance) || southHCP_12_14) &&
	!southH && !southS && !southC && !southBannedDist )

total = hcp(south) + hcp(north)
totalHCP = min_hcp_NS <= total && total <= max_hcp_NS

//Supaya kemungkinan musuh melakukan bid sangat kecil
balancedOpps = shape(east, any 4432 + any 4333) && shape(west, any 4432 + 4333)
balancedHCP = -1 <= (hcp(east) - hcp(west)) && (hcp(east) - hcp(west)) <= 1
conditionEW = (can_bid_EW) ? 1 : balancedOpps && balancedHCP

//Kondisi South - North
conditionSN = southHCP && southDist && totalHCP

condition conditionEW && conditionSN

//----- ..... -----//
